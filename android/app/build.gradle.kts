import java.util.Properties

plugins {
    id("com.android.application")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

// Release signing. Local builds read android/key.properties (git-ignored);
// CI provides the same values via GitHub Secrets as env vars so every
// published APK shares ONE signature and can overwrite-install.
val keystoreProperties = Properties()
val keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(keystorePropertiesFile.inputStream())
}

fun propOrEnv(name: String): String? =
    keystoreProperties.getProperty(name) ?: System.getenv("ANDROID_${name.uppercase()}")

android {
    namespace = "dev.g0spel.zflow"
    // 37 > flutter.compileSdkVersion (36): flutter_secure_storage 11
    // compiles against SDK 37; compileSdk must be the highest of all
    // modules (backward compatible, targetSdk is unaffected).
    compileSdk = 37
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        // Package id: stable install identity so this
        // build installs alongside the original (signatures differ too).
        applicationId = "dev.g0spel.zflow"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        create("release") {
            val storeFileProp = propOrEnv("storeFile")
            if (!storeFileProp.isNullOrBlank()) {
                // Keystore is a PKCS12 file (generated with openssl).
                storeType = "PKCS12"
                keyAlias = propOrEnv("keyAlias")
                keyPassword = propOrEnv("keyPassword")
                storeFile = file(storeFileProp)
                storePassword = propOrEnv("storePassword")
            }
        }
    }

    buildTypes {
        release {
            val releaseSigning = signingConfigs.findByName("release")
            if (releaseSigning != null && releaseSigning.storeFile != null) {
                signingConfig = releaseSigning
            } else {
                // Fall back to debug keys so `flutter build apk` still works
                // when no keystore is configured.
                signingConfig = signingConfigs.getByName("debug")
            }
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
    }
}

flutter {
    source = "../.."
}
